# Vino autoconf

AC_PREREQ([2.64])
AC_INIT([vino],[2.31.4],[http://bugzilla.gnome.org/enter_bug.cgi?product=vino])
AC_CONFIG_SRCDIR(server/vino-fb.c)
AC_CONFIG_HEADERS([config.h])

AM_INIT_AUTOMAKE([1.10])
m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])
AM_MAINTAINER_MODE

dnl make sure we keep ACLOCAL_FLAGS around for maintainer builds to work
AC_SUBST(ACLOCAL_AMFLAGS, "\${ACLOCAL_FLAGS}")

GNOME_COMPILE_WARNINGS([maximum])
GNOME_DEBUG_CHECK
GNOME_MAINTAINER_MODE_DEFINES

LT_PREREQ([2.2.6])
LT_INIT
AC_CONFIG_MACRO_DIR([m4])

GETTEXT_PACKAGE=vino
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [gettext package])
IT_PROG_INTLTOOL([0.40.0])
AM_GLIB_GNU_GETTEXT

AC_PROG_CC
AC_SEARCH_LIBS([strerror],[cposix])
AC_HEADER_STDC
AC_C_BIGENDIAN

AC_PATH_PROG(GCONFTOOL, gconftool-2)
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

GTK_VERSION=2.20.0
GLIB_VERSION=2.17.0
DBUS_VERSION=1.2.3
SOUP_VERSION=2.24.0
TELEPATHY_GLIB_VERSION=0.11.6
NETWORKMANAGER_VERSION=0.7

PKG_CHECK_MODULES(VINO_SERVER, glib-2.0 >= $GLIB_VERSION gtk+-x11-2.0 >= $GTK_VERSION gconf-2.0 dbus-1 >= $DBUS_VERSION dbus-glib-1)

PKG_CHECK_MODULES(VINO_CAPPLET, glib-2.0 >= $GLIB_VERSION gtk+-2.0 >= $GTK_VERSION gconf-2.0 dbus-1 >= $DBUS_VERSION dbus-glib-1 libsoup-2.4 >= $SOUP_VERSION)

PKG_CHECK_MODULES(VINO_TOOLS, glib-2.0 >= $GLIB_VERSION gconf-2.0 gobject-2.0 >= $GLIB_VERSION gnome-keyring-1)

# EGG stuff
PKG_CHECK_MODULES(EGG, gtk+-2.0 >= $GTK_VERSION)
AC_SUBST(EGG_LIBS)
AC_SUBST(EGG_CFLAGS)

PKG_CHECK_MODULES(EGG_SMCLIENT, gtk+-2.0)
AC_SUBST(EGG_SMCLIENT_LIBS)
AC_SUBST(EGG_SMCLIENT_CFLAGS)


# Check for telepathy-glib
AC_ARG_ENABLE(telepathy,
              AS_HELP_STRING([--enable-telepathy=@<:@no/yes/auto@:>@],
                             [Enable telepathy-glib checking]), ,
                             enable_telepathy=auto)

if test "x$enable_telepathy" != "xno"; then
   PKG_CHECK_MODULES(TELEPATHY_GLIB,
   [
      telepathy-glib >= $TELEPATHY_GLIB_VERSION
   ], have_telepathy="yes", have_telepathy="no")
   if test "x$have_telepathy" = "xyes"; then
      AC_SUBST(TELEPATHY_GLIB_CFLAGS)
      AC_SUBST(TELEPATHY_GLIB_LIBS)
      AC_DEFINE(HAVE_TELEPATHY_GLIB, [], [Define if you have telepathy])
   fi
else
   have_telepathy=no
fi

if test "x$enable_telepathy" = "xyes" -a "x$have_telepathy" != "xyes"; then
   AC_MSG_ERROR([Couldn't find telepathy dependencies.])
fi

AM_CONDITIONAL(TELEPATHY_GLIB, test "x$have_telepathy" = "xyes")

dnl --enable-libnotify=(yes|no|auto)
LIBNOTIFY_VERSION=0.4.4

AC_ARG_ENABLE(libnotify,
              AS_HELP_STRING([--enable-libnotify],[use libnotify to display a notification bubble [default=auto]]),,
              enable_libnotify=auto)

if test "x$enable_libnotify" = "xno"; then
    have_libnotify=no
else
    if test "x$enable_libnotify" = "xyes"; then
        PKG_CHECK_MODULES(VINO_LIBNOTIFY, libnotify >= $LIBNOTIFY_VERSION)
        have_libnotify=yes    
    else
        PKG_CHECK_MODULES(VINO_LIBNOTIFY,
                          libnotify >= $LIBNOTIFY_VERSION,
                          have_libnotify=yes, have_libnotify=no)
    fi;
fi

if test "x$have_libnotify" = "xyes" ; then
    AC_DEFINE(VINO_ENABLE_LIBNOTIFY, [], [Set if we should use libnotify])
fi

dnl -- check for NetworkManager -----------------------------------------------
AC_ARG_ENABLE(network-manager,
              AS_HELP_STRING([--enable-network-manager],[use NetworkManager to be notified when network status changes [default=auto]]),
              [if test "x$enableval" = "xno" ; then
                 enable_networkmanager=no
               else
                 enable_networkmanager=yes
               fi],
              enable_networkmanager=auto)

if test "x$enable_networkmanager" = "xno"; then
    have_networkmanager=no
else
    if test "x$enable_networkmanager" = "xyes"; then
        PKG_CHECK_MODULES(VINO_NETWORKMANAGER, NetworkManager >= $NETWORKMANAGER_VERSION)
        have_networkmanager=yes
    else
        PKG_CHECK_MODULES(VINO_NETWORKMANAGER,
                          NetworkManager >= $NETWORKMANAGER_VERSION,
                          have_networkmanager=yes, have_networkmanager=no)
    fi;
fi

if test "x$have_networkmanager" = "xyes" ; then
    AC_DEFINE(VINO_ENABLE_NETWORKMANAGER, [], [Set if we should use NetworkManager])
fi

AC_SUBST(VINO_NETWORKMANAGER_CFLAGS)
AC_SUBST(VINO_NETWORKMANAGER_LIBS)

dnl --enable-gnome-keyring=(yes|no)
AC_ARG_ENABLE(gnome-keyring,
              AS_HELP_STRING([--enable-gnome-keyring],[use gnome keyring for storing password [default=no]]),,
              enable_gnome_keyring=no)
if test "x$enable_gnome_keyring" = "xyes"; then
    PKG_CHECK_MODULES(VINO_KEYRING,
                      gnome-keyring-1,
                      AC_DEFINE(VINO_ENABLE_KEYRING, [], [Set if we should use gnome-keyring]))
fi

dnl --enable-http-server=(yes|no)
AC_ARG_ENABLE(http_server,
	      [  --enable-http-server=[no/yes] enable an HTTP server for serving a Java applet client [default=no]],,
	      enable_http_server=no)
if test "$enable_http_server" = "yes"; then
  VINO_HTTP_CFLAGS="-DVINO_ENABLE_HTTP_SERVER"
else
  VINO_HTTP_CFLAGS=""
fi
AC_SUBST(VINO_HTTP_CFLAGS)
AM_CONDITIONAL(HTTP_SERVER, [test "$enable_http_server" = "yes"])

#
# If Pango included the shared library dependencies from X11 in
# the pkg-config output, then we use that (to avoid duplicates).
# but if they were omitted to avoid binary compatibility problems
# then we need to repeat the checks.
#
if $PKG_CONFIG --exists pangoxft ; then
  PANGO_PACKAGES="pangox pangoxft"
else
  PANGO_PACKAGES="pangox"
fi

x_libs="`$PKG_CONFIG --libs $PANGO_PACKAGES`"
case x_libs in
  *-lX11*) pango_omitted_x_deps=no ;;
  *)       pango_omitted_x_deps=yes ;;
esac

if test $pango_omitted_x_deps = yes ; then
  AC_PATH_XTRA
  
  if test x$no_x = xyes ; then
    AC_MSG_ERROR([X development libraries not found])
  else
    X_LIBS="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"
  fi
fi

AC_SUBST(X_LIBS)

#
# Check for gnutls
#
AC_ARG_ENABLE(gnutls,
	      [  --disable-gnutls=[no/yes] don't build VNC over SSL support [default=no]],,
	      enable_gnutls=yes)
if test "$enable_gnutls" = "yes"; then
  PKG_CHECK_MODULES(LIBGNUTLS, [gnutls >= 1.0.0],
	            [AC_DEFINE(HAVE_GNUTLS, 1, [Defined if the GNU TLS library is present])],
                    AC_MSG_WARN([[
***
*** libgnutls was not found. You may want to get it from
*** ftp://ftp.gnutls.org/pub/gnutls/
]]))
fi

#
# Check for gcrypt
#
AC_ARG_ENABLE(gcrypt,
	      [  --disable-gcrypt=[no/yes] don't use libgcrypt for random number generation [default=no]],,
	      enable_gcrypt=yes)
if test "$enable_gcrypt" = "yes"; then
  AM_PATH_LIBGCRYPT(1.1.90,
                    [AC_DEFINE(HAVE_GCRYPT, 1, [Defined if the libgcrypt library is present])],
                    AC_MSG_WARN([[
***
*** libgcrypt was not found. You may want to get it from
*** ftp://ftp.gnupg.org/pub/gcrypt/alpha/libgcrypt/
]]))
fi

#
# Check for XDAMAGE extension
#
XSHM_LIBS=
AC_CHECK_HEADER(X11/extensions/Xdamage.h, [
    AC_CHECK_LIB(Xdamage, XDamageQueryExtension, [
      AC_DEFINE(HAVE_XDAMAGE, 1, [Defined if the DAMAGE X extension is present])
      XDAMAGE_LIBS="-lXdamage -lXfixes"],, $X_LIBS)
  ],, [#include <X11/Xlib.h>])
AC_SUBST(XDAMAGE_LIBS)

#
# Check for MIT-SHM extension
#
XSHM_LIBS=
AC_CHECK_HEADER(X11/extensions/XShm.h, [
    AC_CHECK_LIB(Xext, XShmQueryExtension, [
      AC_DEFINE(HAVE_XSHM, 1, [Defined if the MIT-SHM X extension is present])
      XSHM_LIBS="-lXext"],, $X_LIBS)
  ],, [#include <X11/Xlib.h>])
AC_SUBST(XSHM_LIBS)

#
# Check for XTest extension
#
XTEST_LIBS=
AC_CHECK_HEADER(X11/extensions/XTest.h, [
    AC_CHECK_LIB(Xtst, XTestQueryExtension, [
      AC_DEFINE(HAVE_XTEST, 1, [Defined if the XTEST X extension is present])
      XTEST_LIBS="-lXtst"],, $X_LIBS)
  ])
AC_SUBST(XTEST_LIBS)

#
# Check for XKB extension
#
AC_CHECK_HEADER(X11/XKBlib.h, [
    AC_CHECK_LIB(X11, XkbQueryExtension,
      [AC_DEFINE(HAVE_XKB, 1, [Defined if the XKB X extension is present])]
      ,, $X_LIBS)
  ])

#
# Check for Avahi
#
AC_ARG_ENABLE(avahi,
        AS_HELP_STRING([--enable-avahi],[Enable use of avahi]),
        [case "${enableval}" in
                yes) VINO_ENABLE_MDNS=yes ;;
                no)  VINO_ENABLE_MDNS=no ;;
                *) AC_MSG_ERROR(bad value ${enableval} for --enable-avahi) ;;
        esac],
        [VINO_ENABLE_MDNS=no])

AVAHI_CFLAGS=
AVAHI_LIBS=
if test "x$VINO_ENABLE_MDNS" = "xyes" ; then
    PKG_CHECK_MODULES(AVAHI, [ avahi-client >= 0.6  avahi-glib >= 0.6 ])
    AVAHI_CFLAGS="$AVAHI_CFLAGS -DVINO_HAVE_AVAHI"
fi
AC_SUBST(AVAHI_CFLAGS)
AC_SUBST(AVAHI_LIBS)

dnl
dnl From libvncserver
dnl

AC_ARG_WITH(jpeg,
        [  --without-jpeg                       disable support for jpeg],
        , [ with_jpeg=yes ])
if test "x$with_jpeg" = "xyes"; then
        AC_CHECK_HEADER(jpeglib.h, HAVE_JPEGLIB_H="true")
fi

AC_ARG_WITH(zlib,
        [  --without-zlib                       disable support for deflate],
        , [ with_zlib=yes ])
AC_ARG_WITH(libz,
        [  --without-libz                       disable support for deflate],
        , [ with_libz=yes ])
if test "x$with_zlib" = "xyes" -a "x$with_libz" = "xyes"; then
        AC_CHECK_HEADER(zlib.h, HAVE_ZLIB_H="true")
fi

if test ! -z "$HAVE_ZLIB_H"; then
        AC_CHECK_LIB(z, deflate, , HAVE_ZLIB_H="")
        if test ! -z "$HAVE_JPEGLIB_H" -a ! -z "$HAVE_ZLIB_H"; then
                AC_CHECK_LIB(jpeg, jpeg_CreateCompress)
        fi
fi

AC_SUBST(LIBZ)
AC_SUBST(LIBJPEG)

AC_CHECK_HEADERS([netinet/in.h sys/time.h fcntl.h unistd.h sys/socket.h signal.h ifaddrs.h net/if.h])
AC_CHECK_FUNCS([gettimeofday])
AC_CHECK_LIB(resolv, herror)

dnl Check for getifaddrs
AM_CONDITIONAL(SELF_IFADDRS, test "x$ac_cv_header_ifaddrs_h" != "xyes")

dnl RFC2553 introduce sockaddr_storage as ifa_addr member in ifaddrs structure
dnl Not all distros follow this.
if test "x$ac_cv_header_ifaddrs_h" = "xyes"; then
        AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[
          #include <ifaddrs.h>
          #include <net/if.h>
          ]], [[
          struct ifaddrs *myaddrs;
          getifaddrs (&myaddrs);
          if (myaddrs->ifa_addr->ss_family == AF_INET) {
          }
        ]])],[have_sockaddr_storage=yes],[have_sockaddr_storage=no])
        if test "x$have_sockaddr_storage" = "xyes"; then
           AC_DEFINE(RFC2553, [], [Define to if follow RFC2553 ])
        fi
fi

dnl
dnl End of libvncserver stuff
dnl


dnl
dnl Check for IPv6 support
dnl
AC_MSG_CHECKING([checking for IPv6 support])
AC_ARG_ENABLE(ipv6,
              AS_HELP_STRING([--enable-ipv6],[Enable ipv6 support [default=yes]]),,
              enable_ipv6=yes)
if test "x$enable_ipv6" = "xyes"; then
    AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[
      #include <sys/types.h>
      #include <sys/socket.h>
    ]], [[
      socket(AF_INET6, SOCK_STREAM, 0);
    ]])],[have_ipv6=yes],[have_ipv6=no])

    if test "x$have_ipv6" = "xyes"; then
       AC_DEFINE(ENABLE_IPV6, [], [Define to enable IPv6 support])
    fi
else
    have_ipv6=no
fi
AC_MSG_RESULT($have_ipv6)

dnl --enable-libunique=(yes|no)
LIBUNIQUE_VERSION=1.0.0

AC_ARG_ENABLE(libunique,
         [  --enable-libunique=[no/yes] Use the library unique to allow only one instance running [default=auto]],,
         enable_libunique=auto)
if test "$enable_libunique" = "no"; then
  have_libunique=no
else
  if test "$enable_libunique" = "yes"; then
    PKG_CHECK_MODULES(VINO_LIBUNIQUE, unique-1.0 >= $LIBUNIQUE_VERSION)
    have_libunique=yes
  else
    PKG_CHECK_MODULES(VINO_LIBUNIQUE,
                      unique-1.0 >= $LIBUNIQUE_VERSION,
                      have_libunique=yes, have_libunique=no)
  fi;
fi

if test "x$have_libunique" = "xyes" ; then
  AC_DEFINE(VINO_ENABLE_LIBUNIQUE, [], [Set if we should use libunique])
fi

AM_GCONF_SOURCE_2

dnl define a MAINT-like variable REBUILD which is set
dnl if Perl, so autogenerated sources can be rebuilt
AC_PATH_PROGS(PERL, perl5 perl)
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

AC_CONFIG_FILES([
Makefile
server/Makefile
server/libvncserver/Makefile
server/libvncserver/ifaddr/Makefile
server/libvncserver/rfb/Makefile
server/miniupnp/Makefile
server/smclient/Makefile
capplet/Makefile
capplet/vino-preferences.desktop.in
tools/Makefile
docs/Makefile
po/Makefile.in
])

AC_OUTPUT

echo "
Configure summary:

	Compiler ...................:  ${CC} 
	Compiler Flags .............:  ${CFLAGS}
	Prefix......................:  ${prefix}
	SysConfDir..................:  ${sysconfdir}

	Avahi support ..............:  ${VINO_ENABLE_MDNS}
	Notify support .............:  ${have_libnotify}
	Network Manager support ....:  ${have_networkmanager}
	GNOME Keyring support.......:  ${enable_gnome_keyring}
	HTTP server.................:  ${enable_http_server}
	Libunique support...........:  ${have_libunique}
	DEBUG messages..............:  ${enable_debug}
	Telepathy support...........:  ${have_telepathy}
"
